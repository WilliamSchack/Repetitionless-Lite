#ifndef REPETITIONLESSNOISE_INCLUDED
#define REPETITIONLESSNOISE_INCLUDED

#include "RepetitionlessTextureUtilities.hlsl"

#include "../Noise/VoronoiNoise2D.hlsl"

// Gets UVs based on voronoi noise
void GetRepetitionlessNoiseUVs(
    float2 UV,

    float NoiseAngleOffset,
    float NoiseScale,
    bool RandomiseNoiseScaling,
    float2 NoiseScalingMinMax,

    bool RandomiseRotation,
    float2 RandomiseRotationMinMax,

    out float VoronoiCells,
    out float EdgeMask,
    out float2 EdgeUV,
    out float2 TransformedUV
){
    // Generate Noise
    float VoronoiDistFromCenter;
    float VoronoiDistFromEdge;
    VoronoiNoise(UV, NoiseAngleOffset, NoiseScale, VoronoiDistFromCenter, VoronoiDistFromEdge, VoronoiCells);
    
    // Scale Edge UVs
    EdgeUV = UV;
    if (RandomiseNoiseScaling) {
        float minMaxAverage = (NoiseScalingMinMax.x + NoiseScalingMinMax.y) / 2;
        EdgeUV *= minMaxAverage;
    }
    
    // Generate Edge Mask, replicating a Sample Gradient Node
    EdgeMask = lerp(0.23, -1.5, VoronoiDistFromEdge) * 5;
    EdgeMask = clamp(EdgeMask, 0, 1);
    
    // Randomise UV Scaling
    TransformedUV = UV;
    if (RandomiseNoiseScaling) {
        float newUVTiling = Remap(VoronoiCells, float2(0, 1), NoiseScalingMinMax);
        TransformedUV *= newUVTiling;
    }
    
    // Rotate UVs
    if (RandomiseRotation) {
        float randomCellDegrees = Remap(VoronoiCells, float2(0, 1), RandomiseRotationMinMax);
        TransformedUV = RotateUVDegrees(TransformedUV, 0.0, randomCellDegrees);
    }
}

// Gets UVs based on a voronoi texture
void GetRepetitionlessNoiseUVs(
    float2 UV,

    float NoiseScale,
    bool RandomiseNoiseScaling,
    float2 NoiseScalingMinMax,

    bool RandomiseRotation,
    float2 RandomiseRotationMinMax,

    UnityTexture2D NoiseTexture,
    int TextureResolution,

    out float VoronoiCells,
    out float EdgeMask,
    out float2 EdgeUV,
    out float2 TransformedUV
){
    // Load data from the noise texture
    float2 noiseTextureData = NoiseTexture.Load(int3((UV.x * NoiseScale) % TextureResolution, (UV.y * NoiseScale) % TextureResolution, 0)).rg;
    VoronoiCells = noiseTextureData.x;
    EdgeMask = noiseTextureData.y;

    // Scale Edge UVs
    EdgeUV = UV;
    if (RandomiseNoiseScaling) {
        float minMaxAverage = (NoiseScalingMinMax.x + NoiseScalingMinMax.y) / 2;
        EdgeUV *= minMaxAverage;
    }

    // Randomise UV Scaling
    TransformedUV = UV;
    if (RandomiseNoiseScaling) {
        float newUVTiling = Remap(VoronoiCells, float2(0, 1), NoiseScalingMinMax);
        TransformedUV *= newUVTiling;
    }
    
    // Rotate UVs
    if (RandomiseRotation) {
        float randomCellDegrees = Remap(VoronoiCells, float2(0, 1), RandomiseRotationMinMax);
        TransformedUV = RotateUVDegrees(TransformedUV, 0.0, randomCellDegrees);
    }
}

// Samples the given texture using modified UVs based on voronoi noise
// Samples the voronoi cells base and edge colour if required and lerps them together
void AddRepetitionlessNoise_float(
    UnityTexture2D InputTexture,
    SamplerState SS,
    float2 UV,

    float NoiseAngleOffset,
    float NoiseScale,
    bool RandomiseNoiseScaling,
    float2 NoiseScalingMinMax,
    
    bool RandomiseRotation, 
    float2 RandomiseRotationMinMax,

    out float4 OutputColor
){
    float VoronoiCells = 1;
    float EdgeMask = 0;
    float2 EdgeUV = UV;
    float2 TransformedUV = UV;
    GetRepetitionlessNoiseUVs(UV, NoiseAngleOffset, NoiseScale, RandomiseNoiseScaling, NoiseScalingMinMax, RandomiseRotation, RandomiseRotationMinMax, VoronoiCells, EdgeMask, EdgeUV, TransformedUV);
    
    OutputColor = SampleRepetitionlessTexture(InputTexture, SS, EdgeMask, EdgeUV, TransformedUV, true);
}

#endif